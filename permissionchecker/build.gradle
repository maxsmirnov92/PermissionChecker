buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

repositories {
    jcenter()
}

apply plugin: 'com.android.library'

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/maxsmirnov92/PermissionChecker'
def gitUrl = 'https://github.com/maxsmirnov92/PermissionChecker.git'

def localReleaseDest = project.hasProperty("LOCAL_REPO") ? "${rootProject.projectDir}/${project.LOCAL_REPO}" : "${rootProject.buildDir}/release/repo"

android {

    signingConfigs {
        release {
            keyAlias 'maxsmirnov'
            storeFile file('~/.android/release-key.keystore')
        }
    }

    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion_

    sourceCompatibility = sourceCompatibilityVersion
    targetCompatibility = sourceCompatibilityVersion

    defaultConfig {
        minSdkVersion minSdkVersion_
        targetSdkVersion sdkVersion
        versionCode 1
        versionName permissionCheckerVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // Don't abort if Lint finds an error, otherwise the Jenkins build
        // will be marked as failed, and Jenkins won't analyse the Lint output
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations {
    javadocDependencies
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    // Support library
    api "com.android.support:appcompat-v7:$supportLibraryVersion"

    // Jetbrains annotations
    api "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupIdName
        pom.artifactId = project.archivesBaseName
        pom.version = permissionCheckerVersion
        repository(url: "file://${localReleaseDest}")
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = true //[Default: false] Whether version should be auto published after an upload
    override = false //[Default: false] Whether to override version artifacts already published
    publications = ['artifactPublication']
    pkg {
        repo = 'maven'
        name = rootProject.name /*+ File.separator + project.name*/
        userOrg = user
        websiteUrl = siteUrl
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = permissionCheckerVersion
        }
        publicDownloadNumbers = true
    }
}

publishing {
    publications {
        artifactPublication(MavenPublication) {
            groupId = groupIdName
            artifactId = project.archivesBaseName
            version = permissionCheckerVersion
            artifact "${project.buildDir}/outputs/aar/${archivesBaseName}-release.aar"
            artifact sourcesJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    if (it.group != null && it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
